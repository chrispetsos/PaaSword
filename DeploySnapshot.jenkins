/*
Jenkins pipeline that deploys snapshot Maven artifacts to
	https://oss.sonatype.org/content/repositories/snapshots/

Also pushes "latest" Docker image to Docker Hub.

This is triggered by a WebHook on GitHub.
*/

node {
	try {
		// notify that the job has started
		notifyBuild('STARTED')
		
		// Define the Docker image variable
		def paaswordSpvImage
	
		// Clone/fetch git repository designated in the pipeline job
		stage('Clone') {
			checkout scm
		}
	
		// Perform Maven tests 
		stage('Test') {
			sh 'mvn clean test'
		}
	
		stage('Build') {
			// Package Maven artifacts
			sh 'mvn package -DskipTests=true'
			// Build Docker image
			// TODO: Automate adding jenkins user to the docker group. This is needed for the build to succeed.
			paaswordSpvImage = docker.build("chrispetsos/paasword-spv")
		}
	
		stage('Deploy') {
			// Deploy Maven artifacts to snapshots repository
			// TODO: Avoid having OSSRH credentials in settings.xml file located in the 
			// jenkins/.m2 folder.
			sh 'mvn deploy -DskipTests=true'
			// Deploy "latest" Docker image to Docker Hub using the docker-hub-credentials created
			// with the Credentials Plugin 
			docker.withRegistry('https://registry.hub.docker.com', 'docker-hub-credentials') {
				paaswordSpvImage.push("latest")
			}
		}
	} catch (e) {
		// If there was an exception thrown, the build failed
		currentBuild.result = "FAILED"
		throw e
	} finally {
		// Success or failure, always send notifications
		notifyBuild(currentBuild.result)
	}
}

def notifyBuild(String buildStatus = 'STARTED') {
	// build status of null means successful
	buildStatus = buildStatus ?: 'SUCCESS'

	// Default values
	def subject = "${buildStatus}: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]'"
	def details = """<p>${buildStatus}: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]':</p>
	<p>Check console output at <a href='${env.BUILD_URL}'>${env.JOB_NAME} [${env.BUILD_NUMBER}]</a></p>"""

	// Send e-mail notification to Default Recipients 
	emailext (
		subject: subject,
		body: details,
		// Jenkins version prior to 2.50 does not honour DEFAULT_RECIPIENTS	
		to: "chrispetsos@gmail.com"
	)
}